import com.google.protobuf.gradle.generateProtoTasks
import com.google.protobuf.gradle.id
import com.google.protobuf.gradle.plugins
import com.google.protobuf.gradle.protobuf
import com.google.protobuf.gradle.protoc

plugins {
    kotlin("jvm") version "1.4.30"
    id("com.google.protobuf") version "0.8.15" //Protocol Buffersを利用するためのプラグイン
    id("idea")
}

repositories {
    gradlePluginPortal()
    //jcenter() deprecatedのためMavenCentral()を記述する必要あり
    google()
    mavenCentral()
}

dependencies {
    implementation(kotlin("stdlib"))
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.2")

    implementation("io.grpc:grpc-kotlin-stub:1.0.0") //gRPCでサーバーと通信するクライアント部分の実装をするkotlinのライブラリ
    implementation("io.grpc:grpc-netty:1.35.0") //NettyでサーバーにgRPCアプリケーションを立ち上げるのに必要なライブラリ

    compileOnly("javax.annotation:javax.annotation-api:1.3.2")
}

//protocでコード生成するためのタスク
protobuf {
    protoc { artifact = "com.google.protobuf:protoc:3.15.1" } //使用するprotocのパッケージとバージョン
    //kotlinのコードがjavaのコードに依存しているところがあるためjavaとkotlin両方必要
    plugins {
        id("grpc") {
            artifact = "io.grpc:protoc-gen-grpc-java:1.36.0" //javaでのgRPCに関するコードを生成するためのプラグイン指定
        }
        id("grpckt") {
            artifact = "io.grpc:protoc-gen-grpc-kotlin:1.0.0:jdk7@jar" //kotlinでのgRPCに関するコードを生成するためのプラグイン指定
        }
    }
    //コード生成タスク
    generateProtoTasks {
        all().forEach {
            it.plugins {
                id("grpc")
                id("grpckt")
            }
        }
    }
}
